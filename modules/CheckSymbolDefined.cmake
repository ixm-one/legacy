include_guard(GLOBAL)

include(CMakePushCheckState)
include(CheckCXXSymbolExists)
include(CheckSymbolExists)
include(ParentScope)
include(Print)

function (check_symbol_defined symbol variable)
  set(option QUIET)
  set(single LANGUAGE)
  set(multi FLAGS DEFINITIONS INCLUDES LIBRARIES HEADERS)
  cmake_parse_arguments(ARG "${option}" "${single}" "${multi}" ${ARGN})

  cmake_push_check_state()
  set(CMAKE_REQUIRED_DEFINITIONS ${ARG_DEFINITIONS})
  set(CMAKE_REQUIRED_LIBRARIES ${ARG_LIBRARIES})
  set(CMAKE_REQUIRED_INCLUDES ${ARG_INCLUDES})
  set(CMAKE_REQUIRED_FLAGS ${ARG_FLAGS})
  set(CMAKE_REQUIRED_QUIET OFF)
  if (ARG_QUIET)
    set(CMAKE_REQUIRED_QUIET ON)
  endif ()
  if (${ARG_LANGUAGE} STREQUAL "CXX")
    check_cxx_symbol_exists(${symbol} ${ARG_HEADERS} ${variable})
  elseif (${ARG_LANGUAGE} STREQUAL "C")
    check_symbol_exists(${symbol} ${ARG_HEADERS} ${variable})
  else ()
    error("LANGUAGE parameter must be either: CXX C")
  endif ()
  cmake_pop_check_state()
  parent_scope(${variable})
endfunction ()
